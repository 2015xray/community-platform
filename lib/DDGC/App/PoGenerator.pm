package DDGC::App::PoGenerator;

use Moose;
use DDGC;
use File::Spec;
use IO::All -utf8;

with qw(
	MooseX::Getopt
);

has targetdir => (
	isa => 'Str',
	is => 'ro',
	required => 1,
);

has context => (
	isa => 'Str',
	is => 'ro',
	predicate => 'has_context',
);

has allcontext => (
	isa => 'Bool',
	is => 'ro',
	default => sub { 0 },
);

has auto => (
	isa => 'Bool',
	is => 'ro',
	default => sub { 1 },
);

has fallback => (
	isa => 'Bool',
	is => 'ro',
	default => sub { 1 },
);

has _ddgc => (
	traits => [qw( NoGetopt )],
	isa => 'DDGC',
	is => 'ro',
	default => sub { DDGC->new },
);
sub d { shift->_ddgc }

has _dir => (
	traits => [qw( NoGetopt )],
	isa => 'Str',
	is => 'ro',
	lazy_build => 1,
);
sub dir { shift->_dir }

sub _build__dir {
	my ( $self ) = @_;
	return $self->targetdir if File::Spec->file_name_is_absolute($self->targetdir);
	return File::Spec->rel2abs($self->targetdir);
}

sub BUILD {
	my ( $self ) = @_;
	$self->error($self->_dir." is not writeable") unless -w $self->_dir;
	if ($self->has_context) {
		$self->error("got context and allcontext at once") if $self->allcontext;
		my $tc = $self->d->rs('Token::Context')->search({ key => $self->context })->first;
		$self->error("no context found") unless $tc;
		$self->generate_pos_for_context($tc,$self->dir);
	} elsif ($self->allcontext) {
		for ($self->d->rs('Token::Context')->search({})->all) {
			my $dir = $self->dir.'/'.$_->key;
			mkdir($dir) unless -d $dir;
			$self->error($dir." is not writeable") unless -w $dir;
			$self->generate_pos_for_context($_,$dir);
		}
	} else {
		$self->error("no context given and no allcontext")
	}
}

sub generate_pos_for_context {
	my ( $self, $token_context, $dir ) = @_;
	for my $tcl ($token_context->token_context_languages->all) {
		$self->generate_po_for_locale($tcl,$dir);
	}
}

sub generate_po_for_locale {
	my ( $self, $token_context_language, $dir ) = @_;
	my $locale = $token_context_language->language->locale;
	my $time = time;
	my $ref = ref $self;
	my $lang = $token_context_language->language->name_in_english;
	my $file = io($dir.'/'.$locale.'.po');
	my $intro = << "EOF";
# Autogenerated by $ref
# language: $lang
# locale: $locale
# time: $time
#
EOF
	$intro > $file;
	for ($token_context_language->search_related('token_languages')->all) {
		$_->auto_use if ($self->auto && !$_->translation);
		$_->gettext_snippet($self->fallback) >> $file;
	}
}

sub error { die "[".(ref shift)."] ".shift }

1;